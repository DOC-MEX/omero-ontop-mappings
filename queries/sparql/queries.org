* Queries against localhost running omero-ontop-mappings
:PROPERTIES:
:ID:       059e06f4-bd2b-4ec1-8280-7c25d62a66fa
:header-args:sparql: :url http://localhost:8080/sparql :cache nil :noweb yes
:mappings: omero-ontop-mappings
:END:
** Prefixes
:PROPERTIES:
:ID:       446e434f-dc84-4edd-b69a-7d96824b853b
:END:
#+name: prefixes
#+begin_src sparql :noweb yes
  PREFIX foaf: <http://xmlns.com/foaf/0.1/>
  PREFIX image: <https://example.org/site/Image/>
  PREFIX dc: <http://purl.org/dc/terms/>
  PREFIX obda: <https://w3id.org/obda/vocabulary#>
  PREFIX omecore: <https://ld.openmicroscopy.org/core/>
  PREFIX omemap: <https://www.openmicroscopy.org/omemap#>
  PREFIX omens: <http://www.openmicroscopy.org/ns/default/>
  PREFIX omekg: <https://ld.openmicroscopy.org/omekg#>
  PREFIX owl: <http://www.w3.org/2002/07/owl#>
  prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
  PREFIX site: <https://example.org/site/>
  PREFIX up: <http://purl.uniprot.org/core/>
  PREFIX uptaxon: <http://purl.uniprot.org/taxonomy/>
  PREFIX vcard: <https://www.w3.org/2006/vcard/ns#>
  PREFIX well: <http://ome.evolbio.mpg.de/Well/>
  PREFIX xml: <http://www.w3.org/XML/1998/namespace>
  PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#+end_src


** SPO
:PROPERTIES:
:ID:       641a4dbb-2590-4db8-a64d-2173554690fd
:END:
#+begin_src sparql
  <<prefixes>>
  select * where {
    ?s ?p ?o .
    }
  limit 10
#+end_src

#+RESULTS:
| s                                                      | p                                               | o                                      |
|--------------------------------------------------------+-------------------------------------------------+----------------------------------------|
| https://ld.openmicroscopy.org/core/last_name           | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |
| https://ld.openmicroscopy.org/core/laser_medium        | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |
| https://ld.openmicroscopy.org/core/type                | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |
| https://ld.openmicroscopy.org/core/uuid                | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |
| https://ld.openmicroscopy.org/core/pockel_cell_setting | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |
| https://ld.openmicroscopy.org/core/k                   | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |
| https://ld.openmicroscopy.org/core/metadata_only       | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |
| https://ld.openmicroscopy.org/core/reagent_identifier  | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |
| https://ld.openmicroscopy.org/core/font_size           | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |
| https://ld.openmicroscopy.org/core/temperature         | http://www.w3.org/1999/02/22-rdf-syntax-ns#type | https://w3id.org/linkml/SlotDefinition |


** Project
:PROPERTIES:
:ID:       c97f7deb-8163-4c3d-9c8f-ed50b3e36552
:END:
#+begin_src sparql
  <<prefixes>>
  select distinct  ?project ?typ where {
  ?project a omecore:Project ;
      rdf:type ?typ ;
  }
  limit 10
#+end_src

#+RESULTS:
| project                            | typ                                        |
|------------------------------------+--------------------------------------------|
| https://example.org/site/Project/1 | https://ld.openmicroscopy.org/core/Project |


** Dataset
:PROPERTIES:
:ID:       c97f7deb-8163-4c3d-9c8f-ed50b3e36552
:END:
*** List all datasets
:PROPERTIES:
:ID:       eda1ed27-583d-4637-bc2b-c8795a408c9b
:END:
#+begin_src sparql
  <<prefixes>>
  select ?ds where {
  ?ds a omecore:Dataset .
  }
  limit 10
#+end_src

#+RESULTS:
| ds                                 |
|------------------------------------|
| https://example.org/site/Dataset/2 |
| https://example.org/site/Dataset/3 |
| https://example.org/site/Dataset/1 |


*** datasets and properties
:PROPERTIES:
:ID:       ffeb2f65-35a2-40b6-af88-820caf58ffa8
:END:
#+begin_src sparql
  <<prefixes>>
  select * where {
  ?ds a omecore:Dataset ;
      omecore:experimenter ?owner .
  }
  order by ?owner
#+end_src

#+RESULTS:
| ds                                 | owner                                   |
|------------------------------------+-----------------------------------------|
| https://example.org/site/Dataset/1 | https://example.org/site/Experimenter/0 |
| https://example.org/site/Dataset/2 | https://example.org/site/Experimenter/0 |
| https://example.org/site/Dataset/3 | https://example.org/site/Experimenter/0 |


** Dataset II
:PROPERTIES:
:ID:       c97f7deb-8163-4c3d-9c8f-ed50b3e36552
:END:
#+begin_src sparql

  <<prefixes>>
  select distinct * where {
  ?ds a omecore:Dataset .
  ?ds ?prop ?value .
  }
#+end_src

#+RESULTS:
| ds                                 | prop                                                     | value                                        |
|------------------------------------+----------------------------------------------------------+----------------------------------------------|
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/6             |
| https://example.org/site/Dataset/1 | http://purl.org/dc/terms/subject                         | Test images                                  |
| https://example.org/site/Dataset/3 | http://purl.org/dc/terms/provenance                      | Screenshots                                  |
| https://example.org/site/Dataset/1 | https://ld.openmicroscopy.org/omekg#experimenter_group   | https://example.org/site/ExperimenterGroup/0 |
| https://example.org/site/Dataset/3 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/10            |
| https://example.org/site/Dataset/1 | http://www.w3.org/1999/02/22-rdf-syntax-ns#type          | https://ld.openmicroscopy.org/core/Dataset   |
| https://example.org/site/Dataset/1 | http://purl.org/dc/elements/1.1/identifier               | 2                                            |
| https://example.org/site/Dataset/3 | http://www.w3.org/1999/02/22-rdf-syntax-ns#type          | https://ld.openmicroscopy.org/core/Dataset   |
| https://example.org/site/Dataset/1 | https://ld.openmicroscopy.org/omekg#experimenter         | https://example.org/site/Experimenter/0      |
| https://example.org/site/Dataset/3 | https://ld.openmicroscopy.org/omekg#experimenter_group   | https://example.org/site/ExperimenterGroup/0 |
| https://example.org/site/Dataset/2 | http://www.w3.org/1999/02/22-rdf-syntax-ns#type          | https://ld.openmicroscopy.org/core/Dataset   |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/11            |
| https://example.org/site/Dataset/3 | http://purl.org/dc/elements/1.1/identifier               | 3                                            |
| https://example.org/site/Dataset/3 | http://www.w3.org/2000/01/rdf-schema#label               | Dataset 3                                    |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/experimenter          | https://example.org/site/Experimenter/0      |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/3             |
| https://example.org/site/Dataset/3 | https://ld.openmicroscopy.org/core/experimenter_group    | https://example.org/site/ExperimenterGroup/0 |
| https://example.org/site/Dataset/1 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/2             |
| https://example.org/site/Dataset/2 | http://purl.org/dc/elements/1.1/identifier               | 4                                            |
| https://example.org/site/Dataset/1 | http://purl.org/dc/elements/1.1/identifier               | 1                                            |
| https://example.org/site/Dataset/1 | http://purl.org/dc/terms/contributor                     | Test User                                    |
| https://example.org/site/Dataset/3 | http://purl.org/dc/terms/contributor                     | Caligula                                     |
| https://example.org/site/Dataset/3 | https://ld.openmicroscopy.org/core/group                 | https://example.org/site/ExperimenterGroup/0 |
| https://example.org/site/Dataset/2 | http://purl.org/dc/elements/1.1/identifier               | 2                                            |
| https://example.org/site/Dataset/3 | https://ld.openmicroscopy.org/omekg#experimenter         | https://example.org/site/Experimenter/0      |
| https://example.org/site/Dataset/1 | https://ld.openmicroscopy.org/core/experimenter          | https://example.org/site/Experimenter/0      |
| https://example.org/site/Dataset/2 | http://purl.org/dc/terms/contributor                     | Test User                                    |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/omekg#experimenter         | https://example.org/site/Experimenter/0      |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/experimenter_group    | https://example.org/site/ExperimenterGroup/0 |
| https://example.org/site/Dataset/1 | http://purl.org/dc/terms/provenance                      | Screenshots                                  |
| https://example.org/site/Dataset/2 | http://purl.org/dc/elements/1.1/identifier               | 3                                            |
| https://example.org/site/Dataset/3 | https://ld.openmicroscopy.org/core/experimenter          | https://example.org/site/Experimenter/0      |
| https://example.org/site/Dataset/1 | http://www.w3.org/2000/01/rdf-schema#label               | Dataset 1                                    |
| https://example.org/site/Dataset/3 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/9             |
| https://example.org/site/Dataset/1 | https://ld.openmicroscopy.org/omekg#tag_annotation_value | TestTag                                      |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/7             |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/omekg#experimenter_group   | https://example.org/site/ExperimenterGroup/0 |
| https://example.org/site/Dataset/2 | http://purl.org/dc/terms/provenance                      | Screenshots                                  |
| https://example.org/site/Dataset/1 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/1             |
| https://example.org/site/Dataset/2 | http://purl.org/dc/elements/1.1/identifier               | 5                                            |
| https://example.org/site/Dataset/2 | http://purl.org/dc/elements/1.1/identifier               | 7                                            |
| https://example.org/site/Dataset/3 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/8             |
| https://example.org/site/Dataset/2 | http://purl.org/dc/elements/1.1/identifier               | 6                                            |
| https://example.org/site/Dataset/2 | http://purl.org/dc/elements/1.1/identifier               | 12                                           |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/5             |
| https://example.org/site/Dataset/2 | http://purl.org/dc/terms/subject                         | Test images                                  |
| https://example.org/site/Dataset/3 | http://purl.org/dc/elements/1.1/identifier               | 8                                            |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/group                 | https://example.org/site/ExperimenterGroup/0 |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/4             |
| https://example.org/site/Dataset/1 | https://ld.openmicroscopy.org/core/group                 | https://example.org/site/ExperimenterGroup/0 |
| https://example.org/site/Dataset/3 | http://purl.org/dc/elements/1.1/identifier               | 9                                            |
| https://example.org/site/Dataset/1 | https://ld.openmicroscopy.org/core/experimenter_group    | https://example.org/site/ExperimenterGroup/0 |
| https://example.org/site/Dataset/2 | http://www.w3.org/2000/01/rdf-schema#label               | Dataset 2                                    |
| https://example.org/site/Dataset/3 | http://purl.org/dc/terms/subject                         | OMERO Mapping                                |
| https://example.org/site/Dataset/3 | http://purl.org/dc/elements/1.1/identifier               | 10                                           |
| https://example.org/site/Dataset/2 | http://purl.org/dc/elements/1.1/identifier               | 11                                           |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/image                 | https://example.org/site/Image/12            |


** Project Dataset Image hierarchy
:PROPERTIES:
:ID:       9a7fae93-80a0-4cf9-b889-a60113b9bf01
:END:
#+begin_src sparql

  <<prefixes>>
  SELECT distinct *  WHERE {
      ?project a omecore:Project ;
                omecore:dataset ?dataset .
      ?dataset a omecore:Dataset ;
                omecore:image ?image .
      ?image a omecore:Image ;
              rdfs:label ?image_name .
  }
#+end_src

#+RESULTS:
| project                            | dataset                            | image                             | image_name                                 |
|------------------------------------+------------------------------------+-----------------------------------+--------------------------------------------|
| https://example.org/site/Project/1 | https://example.org/site/Dataset/1 | https://example.org/site/Image/2  | 2024-10-10_14-53-28_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/1 | https://example.org/site/Image/1  | 2024-10-10_14-58-36_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/6  | 2024-10-10_15-01-36_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/5  | 2024-10-10_15-07-18_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/4  | 2024-10-10_15-09-28_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/7  | 2024-10-10_15-17-25_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/3  | 2024-10-10_15-28-16_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/11 | 2024-10-10_15-28-16_screenshot.png.ome.tif |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/3 | https://example.org/site/Image/9  | 2024-10-10_16-39-27_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/3 | https://example.org/site/Image/10 | 2024-10-10_16-42-47_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/3 | https://example.org/site/Image/8  | 2024-10-10_16-47-01_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/12 | image_6_with_roi.ome.tif                   |


** Image with owner
:PROPERTIES:
:ID:       c97f7deb-8163-4c3d-9c8f-ed50b3e36552
:END:
#+begin_src sparql
  <<prefixes>>
  select distinct * where {
  ?ds a omecore:Image ;
      omecore:experimenter ?owner .
  }
#+end_src

#+RESULTS:
| ds                                | owner                                   |
|-----------------------------------+-----------------------------------------|
| https://example.org/site/Image/1  | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/10 | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/11 | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/12 | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/2  | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/3  | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/4  | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/5  | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/6  | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/7  | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/8  | https://example.org/site/Experimenter/0 |
| https://example.org/site/Image/9  | https://example.org/site/Experimenter/0 |
| https://example.org/site/ROI/1    | https://example.org/site/Experimenter/0 |
| https://example.org/site/ROI/2    | https://example.org/site/Experimenter/0 |



** Image and DC metadata terms
:PROPERTIES:
:ID:       f9c6719d-7aad-460a-8200-def2533884dd
:END:
#+begin_src sparql

  <<prefixes>>
  SELECT distinct ?img ?author ?subject WHERE {
    ?img a omecore:Image;
         dc:contributor ?author;
         dc:subject ?subject .
    }
#+end_src

#+RESULTS:
| img                               | author    | subject  |
|-----------------------------------+-----------+----------|
| https://example.org/site/Image/1  | Test User | Unittest |
| https://example.org/site/Image/2  | Test User | Unittest |
| https://example.org/site/Image/3  | Test User | Unittest |
| https://example.org/site/Image/4  | Test User | Unittest |
| https://example.org/site/Image/5  | Test User | Unittest |
| https://example.org/site/Image/6  | Test User | Unittest |
| https://example.org/site/Image/7  | Test User | Unittest |
| https://example.org/site/Image/8  | Test User | Unittest |
| https://example.org/site/Image/9  | Test User | Unittest |
| https://example.org/site/Image/10 | Test User | Unittest |
| https://example.org/site/Image/11 | Test User | Unittest |
| https://example.org/site/Image/12 | Test User | Unittest |



** Image properties
:PROPERTIES:
:ID:       5fd7fb92-7286-4fb7-bb0b-e34962e20957
:END:
#+begin_src sparql
  <<prefixes>>
  SELECT distinct ?prop ?val WHERE {
    image:1 ?prop ?val .
    }
  order by ?val
#+end_src

#+RESULTS:
| prop                                                     | val                                          |
|----------------------------------------------------------+----------------------------------------------|
| https://ld.openmicroscopy.org/omekg#experimenter         | https://example.org/site/Experimenter/0      |
| https://ld.openmicroscopy.org/core/experimenter          | https://example.org/site/Experimenter/0      |
| https://ld.openmicroscopy.org/core/group                 | https://example.org/site/ExperimenterGroup/0 |
| https://ld.openmicroscopy.org/omekg#experimenter_group   | https://example.org/site/ExperimenterGroup/0 |
| https://ld.openmicroscopy.org/core/experimenter_group    | https://example.org/site/ExperimenterGroup/0 |
| http://www.w3.org/1999/02/22-rdf-syntax-ns#type          | https://ld.openmicroscopy.org/core/Image     |
| http://www.w3.org/2000/01/rdf-schema#label               | 2024-10-10_14-58-36_screenshot.png           |
| http://www.openmicroscopy.org/ns/default/fooZZbar        | bar                                          |
| http://www.openmicroscopy.org/ns/default/ZZZZfooZZZZbar  | cool                                         |
| https://ld.openmicroscopy.org/omekg#tag_annotation_value | Screenshot                                   |
| http://www.openmicroscopy.org/ns/default/ZZbarZZba       | some**thing                                  |
| http://purl.org/dc/terms/contributor                     | Test User                                    |
| http://purl.org/dc/terms/date                            | Thu Sep 11 11:49:01 AM CEST 2025             |
| http://purl.org/dc/terms/subject                         | Unittest                                     |
| http://purl.org/dc/elements/1.1/identifier               | 1                                            |

** Dataset type relations
:PROPERTIES:
:ID:       c97f7deb-8163-4c3d-9c8f-ed50b3e36552
:END:
#+begin_src sparql
  <<prefixes>>
      select ?s ?tp  where {
          ?s a omecore:Dataset;
              a ?tp .
  }
  order by ?tp
#+end_src

#+RESULTS:
| s                                  | tp                                         |
|------------------------------------+--------------------------------------------|
| https://example.org/site/Dataset/1 | https://ld.openmicroscopy.org/core/Dataset |
| https://example.org/site/Dataset/2 | https://ld.openmicroscopy.org/core/Dataset |
| https://example.org/site/Dataset/3 | https://ld.openmicroscopy.org/core/Dataset |


** Dataset number of type relations
:PROPERTIES:
:ID:       c97f7deb-8163-4c3d-9c8f-ed50b3e36552
:END:
#+begin_src sparql
  <<prefixes>>
      select (count(distinct ?tp) as ?n_types) where {
          ?s a omecore:Dataset;
              a ?tp .
  }

#+end_src

#+RESULTS:
| n_types |
|---------|
|       1 |

** Count number of images in datasets.
:PROPERTIES:
:ID:       9f3ccdc1-85b7-4cf4-a3fb-8a34401b96b5
:END:
#+begin_src sparql
  <<prefixes>>
  SELECT ?dataset (count(distinct ?img) as ?Nimg) 
  WHERE {
    ?img a omecore:Image ;
         ^omecore:image ?dataset . # Uses inverse property.
    ?dataset a omecore:Dataset .
  }
  group by ?dataset
  order by desc(?Nimg) # Descending sort by number of images.
#+end_src

#+RESULTS:
| dataset                            | Nimg |
|------------------------------------+------|
| https://example.org/site/Dataset/2 |    7 |
| https://example.org/site/Dataset/3 |    3 |
| https://example.org/site/Dataset/1 |    2 |



** Number of projects, datasets, images
:PROPERTIES:
:ID:       b8d9a7e6-cb6f-46a3-a198-f8a57a3e81ba
:END:
#+begin_src sparql

  <<prefixes>>
  select ?n_projects ?n_datasets ?n_images where {
    {
      select (count(?project) as ?n_projects) where {
        ?project a omecore:Project .
      }
    }
    {
      select (count(?dataset) as ?n_datasets) where {
        ?dataset a omecore:Dataset .
      }
    }
    {
      select (count(?image) as ?n_images) where {
        ?image a omecore:Image .
      }
    }
  }
#+end_src

#+RESULTS:
| n_projects | n_datasets | n_images |
|------------+------------+----------|
|          1 |          3 |       14 |


** Project and contained datasets
:PROPERTIES:
:ID:       9114c7b4-6367-43f6-a8d2-9583999e554f
:END:
#+begin_src sparql

  <<prefixes>>
    select * where {
    ?proj a omecore:Project ;
             rdfs:label ?lbl .
    ?ds a omecore:Dataset .
    ?proj omecore:dataset ?ds .
    }
  limit 20
#+end_src

#+RESULTS:
| proj                               | lbl     | ds                                 |
|------------------------------------+---------+------------------------------------|
| https://example.org/site/Project/1 | Project | https://example.org/site/Dataset/1 |
| https://example.org/site/Project/1 | Project | https://example.org/site/Dataset/2 |
| https://example.org/site/Project/1 | Project | https://example.org/site/Dataset/3 |


** Project with datasets and images
:PROPERTIES:
:ID:       9114c7b4-6367-43f6-a8d2-9583999e554f
:END:
#+begin_src sparql

  <<prefixes>>
    select distinct * where {
    ?proj a omecore:Project ;
      omecore:dataset ?dataset .
    ?dataset a omecore:Dataset ;
      omecore:image ?image .
    ?image a omecore:Image ;
      rdfs:label ?name .
    }
  order by ?image ?dataset ?proj
  limit 20

#+end_src

#+RESULTS:
| proj                               | dataset                            | image                             | name                                       |
|------------------------------------+------------------------------------+-----------------------------------+--------------------------------------------|
| https://example.org/site/Project/1 | https://example.org/site/Dataset/1 | https://example.org/site/Image/1  | 2024-10-10_14-58-36_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/3 | https://example.org/site/Image/10 | 2024-10-10_16-42-47_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/11 | 2024-10-10_15-28-16_screenshot.png.ome.tif |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/12 | image_6_with_roi.ome.tif                   |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/1 | https://example.org/site/Image/2  | 2024-10-10_14-53-28_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/3  | 2024-10-10_15-28-16_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/4  | 2024-10-10_15-09-28_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/5  | 2024-10-10_15-07-18_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/6  | 2024-10-10_15-01-36_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/2 | https://example.org/site/Image/7  | 2024-10-10_15-17-25_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/3 | https://example.org/site/Image/8  | 2024-10-10_16-47-01_screenshot.png         |
| https://example.org/site/Project/1 | https://example.org/site/Dataset/3 | https://example.org/site/Image/9  | 2024-10-10_16-39-27_screenshot.png         |


** Dataset subject as per map annotation, queried by namespace:key concatenation (dc:subject)
:PROPERTIES:
:ID:       39bce638-19c5-4ed5-9428-7bfdbdc64b72
:END:
#+begin_src sparql
  <<prefixes>>
  SELECT distinct ?ds ?subject WHERE {
      ?ds a omecore:Dataset;
           dc:subject ?subject .
   }
  order by ?img
#+end_src

#+RESULTS:
| ds                                 | subject       |
|------------------------------------+---------------|
| https://example.org/site/Dataset/1 | Test images   |
| https://example.org/site/Dataset/2 | Test images   |
| https://example.org/site/Dataset/3 | OMERO Mapping |


** Images and (optional) tags
:PROPERTIES:
:ID:       3fb29f13-6b99-4d93-9757-7b6d90a40e93
:END:

#+begin_src sparql


  <<prefixes>>
  SELECT distinct ?img ?tag WHERE {
      ?img a omecore:Image.
      ?img omekg:tag_annotation_value ?tag .
   }
#+end_src

#+RESULTS:
| img                               | tag        |
|-----------------------------------+------------|
| https://example.org/site/Image/1  | Screenshot |
| https://example.org/site/Image/2  | Screenshot |
| https://example.org/site/Image/3  | Screenshot |
| https://example.org/site/Image/4  | Screenshot |
| https://example.org/site/Image/5  | Screenshot |
| https://example.org/site/Image/6  | Screenshot |
| https://example.org/site/Image/7  | Screenshot |
| https://example.org/site/Image/8  | Screenshot |
| https://example.org/site/Image/9  | Screenshot |
| https://example.org/site/Image/10 | Screenshot |
| https://example.org/site/Image/11 | Screenshot |
| https://example.org/site/Image/12 | Screenshot |


** Tagged dataset
:PROPERTIES:
:ID:       5ccad4e1-5090-438e-b90c-ede0bd3356bc
:END:

Find all datasets tagged "TestTag".

#+begin_src sparql


  <<prefixes>>
  SELECT distinct ?ds ?name ?tag WHERE {
    ?ds a omecore:Dataset;
        omekg:tag_annotation_value ?tag .
    filter(regex(?tag, "^TestTag$"))
    ?ds rdfs:label ?name .
   }
  order by ?img
#+end_src

#+RESULTS:
| ds                                 | name      | tag     |
|------------------------------------+-----------+---------|
| https://example.org/site/Dataset/1 | Dataset 1 | TestTag |





** Property with default namespace
:PROPERTIES:
:ID:       f538ab93-67f7-4a3e-aa6e-9b6d82e2f99c
:END:

#+begin_src sparql

  <<prefixes>>
  SELECT DISTINCT * WHERE {
    image:11 omens:sampletype ?val.
   }
#+end_src

#+RESULTS:
| val    |
|--------|
| screen |


** Image 9 MouseCT/Skyscan/System namespace
:PROPERTIES:
:ID:       d2ec4a2a-806a-45ae-95a0-3b36d16aa030
:END:
#+begin_src sparql

  <<prefixes>>
  SELECT DISTINCT * WHERE {
    image:9 ?prop ?val .
   }
#+end_src

#+RESULTS:
| prop                                                     | val                                          |
|----------------------------------------------------------+----------------------------------------------|
| http://www.w3.org/1999/02/22-rdf-syntax-ns#type          | https://ld.openmicroscopy.org/core/Image     |
| https://ld.openmicroscopy.org/core/experimenter          | https://example.org/site/Experimenter/0      |
| http://purl.org/dc/terms/date                            | Thu Sep 11 11:49:26 AM CEST 2025             |
| http://purl.org/dc/elements/1.1/identifier               | 9                                            |
| http://www.w3.org/2000/01/rdf-schema#label               | 2024-10-10_16-39-27_screenshot.png           |
| https://ld.openmicroscopy.org/core/group                 | https://example.org/site/ExperimenterGroup/0 |
| https://ld.openmicroscopy.org/omekg#tag_annotation_value | Screenshot                                   |
| http://purl.org/dc/terms/contributor                     | Test User                                    |
| http://purl.org/dc/terms/subject                         | Unittest                                     |
| http://www.openmicroscopy.org/ns/default/Assay           | Bruker                                       |



** Owners and Groups
:PROPERTIES:
:ID:       83f91f9d-a78e-44a0-ae4c-f24046beaa26
:END:

#+begin_src sparql

  <<prefixes>>
  SELECT distinct * WHERE {
      ?s a omecore:Project ;
  omecore:experimenter ?owner;
  omecore:experimenter_group ?group .
  }
 #+end_src

 #+RESULTS:
 | s | owner | group |
 |---+-------+-------|


#+begin_src sparql :url http://localhost:8080/sparql

  <<prefixes>>
  SELECT distinct ?group ?name WHERE {
    ?group a omekg:Group;
            foaf:name ?name .
    }
 #+end_src


** Federated query against ome and taxonomy database with HCS objects
:PROPERTIES:
:ID:       94881a24-726a-40ee-b931-d46a8e31a2f1
:END:

#+begin_src sparql :url http://localhost:8080/sparql

  <<prefixes>>
  SELECT distinct * WHERE {
    ?well a omekg:Well ;
    <http://www.openmicroscopy.org/ns/default/TermSource1Accession> ?taxid.

    bind(strafter(?taxid, 'NCBITaxon_') as ?taxon_id)
    bind(iri(concat("http://purl.uniprot.org/taxonomy/", ?taxon_id)) as ?up_taxon)

  # service <https://sparql.uniprot.org/sparql> {
  #   ?up_taxon ?prop ?val .
  # }
  }
  limit 10
 #+end_src

#+begin_src sparql :url https://sparql.uniprot.org/sparql :async yes
  <<prefixes>>
  SELECT *
  FROM <http://sparql.uniprot.org/taxonomy>
  WHERE
  {
     <http://purl.uniprot.org/taxonomy/9606> up:mnemonic ?mnemonic;
                                             ^up:host ?parasite .
    ?parasite up:scientificName ?parasite_name .
  }
  limit 100
#+end_src


** Federated query against locally running qlever and uniprot
:PROPERTIES:
:ID:       deea35fd-873a-4a43-983e-6497a8a04163
:END:
#+begin_src sparql :url http://localhost:8888/sparql 


  <<prefixes>>
  SELECT DISTINCT ?well ?taxon ?host_scientific_name ?guest_organism_scientificname WHERE {
    ?well a omekg:Well ;
          omens:TermSource1Accession ?taxon .
    BIND (IRI(CONCAT(STR(uptaxon:), STRAFTER(?taxon,"NCBITaxon_"))) AS ?host)
    SERVICE <https://sparql.uniprot.org/sparql> {
      ?host up:scientificName ?host_scientific_name;
  	^up:host ?guest_organism .
      ?guest_organism up:scientificName ?guest_organism_scientificname .
    }
  }
  LIMIT 100
#+end_src



